# =============================================================================================
# ~/.config/zsh/.zshrc                                                                        #
# =============================================================================================
# Instructions to be executed when a new ZSH session is launched                              #
# Imports all plugins, aliases, helper functions, and configurations                          #
#                                                                                             #
# After editing, re-source .zshrc for new changes to take effect                              #
# For docs and more info, see: https://github.com/wilfriedago/dotfiles                        #
# =============================================================================================
# License: MIT Copyright (c) 2025 Wilfried Kirin AGO <https://wilfriedago.me>                 #
# =============================================================================================

# Download Zinit, if it's not there yet
[[ ! -d "$ZINIT_INSTALL_DIR" ]] && git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_INSTALL_DIR"

# Load Zinit
source "${ZINIT_INSTALL_DIR}/zinit.zsh"

# Load zinit's completion system
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# Load starship theme
zi ice \
  as"command" \
  from"gh-r" \
  atclone"./starship init zsh > starship.zsh; ./starship completions zsh > _starship" \
  atpull"%atclone" \
  src"starship.zsh"
zi light starship/starship

# Plugins — turbo mode defers loading until after first prompt for faster startup
zi ice wait lucid
zi light aloxaf/fzf-tab

zi ice wait lucid atload"_zsh_autosuggest_start"
zi light zsh-users/zsh-autosuggestions

zi ice wait lucid atinit"zicdreplay"
zi light zdharma-continuum/fast-syntax-highlighting

# Snippets
zi snippet https://github.com/lincheney/fzf-tab-completion/blob/master/zsh/fzf-zsh-completion.sh

# =============================================================================================
# Shell
# =============================================================================================

[ -f "$ZSH_CONFIG_DIR/aliases.zsh" ] && source "$ZSH_CONFIG_DIR/aliases.zsh" # Aliases
[ -f "$ZSH_CONFIG_DIR/settings.zsh" ] && source "$ZSH_CONFIG_DIR/settings.zsh" # Settings
[ -f "$ZSH_CONFIG_DIR/functions.zsh" ] && source "$ZSH_CONFIG_DIR/functions.zsh" # Functions
[ -f "$ZSH_CONFIG_DIR/completions.zsh" ] && source "$ZSH_CONFIG_DIR/completions.zsh" # Completions

# =============================================================================================
# Completion initialization — must run BEFORE plugins that call compdef
# =============================================================================================
COMPDUMP="${ZSH_CACHE_DIR}/.zcompdump"
autoload -Uz compinit
if [[ -n "$COMPDUMP"(#qN.mh+24) ]]; then
  compinit -d "$COMPDUMP"
else
  compinit -C -d "$COMPDUMP"
fi

[ -d "$ZSH_CONFIG_DIR/plugins" ] && for plugin in "$ZSH_CONFIG_DIR/plugins"/*; do source "$plugin"; done # Plugins

# Compile compdump in background if stale
if [[ -s "$COMPDUMP" && (! -s "${COMPDUMP}.zwc" || "$COMPDUMP" -nt "${COMPDUMP}.zwc") ]]; then
  zcompile "$COMPDUMP"
fi
